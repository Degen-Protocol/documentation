# GOAT Token

Deployed at `0xF5D0140B4d53c9476DC1488BC6d8597d7393f074`

```solidity filename="GoatToken.sol" {3} copy
// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "./IWPLS.sol";

import "./SafeERC20.sol";

import "./ReentrancyGuard.sol";

contract GoatToken is IERC20, ReentrancyGuard {
    using SafeMath for uint256;
    using SafeERC20 for IERC20;

    IERC20 public immutable WPLS;
    IWPLS public immutable WRAP;

    // token data
    string private constant _name = "Degen Protocol GOAT";
    string private constant _symbol = "GOAT";
    uint8 private constant _decimals = 18;
    uint256 private constant precision = 10**18;

    uint256  private _total_users;
    uint256  private _total_txs;
    uint256 private _totalSupply;

    // storage
    struct User {
        uint256 lastEntryPrice; // Ratio at time of last deposit
        uint256 deposited;  // Deposited WPLS, from minting
        uint256 withdrawn;  // Withdrawn WPLS, from redemptions
    }

    // balances
    mapping (address => User) private _stats;
    mapping (address => uint8) private _users;
    mapping (address => uint256) private _balances;
    mapping (address => mapping (address => uint256)) private _allowances;

    
    // 5% mint and redeem Fees
    uint256 public mintFee = 95000;            
    uint256 public sellFee = 95000;            
    uint256 private constant feeDenominator = 10**5;

    // Events
    
    event Burn(address from, uint256 amountBurned);
    event GarbageCollected(uint256 amountBurned);
    
    event Minted(address recipient, uint256 amountTKN);
    event Redeemed(address seller, uint256 amountTKN, uint256 amountBASE);
    event PriceChange(uint256 previousPrice, uint256 currentPrice, uint256 totalSupply);

    ////////////////////////////
    // CONSTRUCTOR & FALLBACK //
    ////////////////////////////

    constructor(address _base) {
        WPLS = IERC20(address(_base));
        WRAP = IWPLS(address(_base));
    }

    // WPLS sent to this contract gets converted into WPLS
    // Then, WPLS is deposited and token is returned to the sender
    receive () external payable {
        address sender = msg.sender;
        uint256 amount = msg.value;
        
        WRAP.deposit{value: amount}();

        _checkGarbageCollector(address(this));

        _mintWithBacking(amount, sender);
    }

    ////////////////////
    // VIEW FUNCTIONS //
    ////////////////////

    // Token Name
    function name() public pure override returns (string memory) {
        return _name;
    }

    // Token Ticker Symbol
    function symbol() public pure override returns (string memory) {
        return _symbol;
    }

    // Tokens decimals
    function decimals() public pure override returns (uint8) {
        return _decimals;
    }

    // Returns the total number of tokens in existence
    function totalSupply() external view override returns (uint256) { 
        return _totalSupply; 
    }

    // Returns the number of tokens owned by `account`
    function balanceOf(address account) public view override returns (uint256) { 
        return _balances[account]; 
    }

    // Returns the number of tokens `spender` can transfer from `holder`
    function allowance(address holder, address spender) external view override returns (uint256) { 
        return _allowances[holder][spender]; 
    }

    // Returns the amount of token given numtokens of WPLS; accounting for fees
    function estimateMinted(uint256 numTokens) public view returns (uint256) {
        uint balance = underlyingBalance();
        return _totalSupply == 0 ? numTokens.mul(mintFee).div(feeDenominator) : _totalSupply.mul(numTokens).div(balance).mul(mintFee).div(feeDenominator);
    }

    // Returns the amount of WPLS given numtokens of token
    function estimateRedeemed(uint256 numTokens) public view returns (uint256) {
        return amountOut(numTokens.mul(sellFee).div(feeDenominator));
    }

    // Amount Of Underlying To Receive For `numTokens` of token
    function amountOut(uint256 numTokens) public view returns (uint256) {
        return _calculatePrice().mul(numTokens).div(precision);
    }

    // Returns the value of `holder`'s holdings
    function getValueOfHoldings(address holder) public view returns(uint256) {
        return amountOut(_balances[holder]);
    }

    // Underlying WPLS balance
    function underlyingBalance() public view returns (uint256) {
        return WPLS.balanceOf(address(this));
    }

    // Price Of token in WPLS
    function calculatePrice() public view returns (uint256) {
        return _calculatePrice();
    }

    // Returns basic contract stats
    function getInfo() public view returns(uint256 users, uint256 txs, uint256 underlyingSupply, uint256 supply, uint256 price){
        users = _total_users;
        txs = _total_txs;
        underlyingSupply = underlyingBalance();
        supply = _totalSupply;
        price = _calculatePrice();
    }

    // Last price a user minted at
    function getLastEntryOf(address user) public view returns (uint256) {
        return _stats[user].lastEntryPrice;
    }

    // Total WPLS deposited
    function getDepositedOf(address user) public view returns (uint256) {
        return _stats[user].deposited;
    }

    // Total WPLS withdrawn
    function getWithdrawnOf(address user) public view returns (uint256) {
        return _stats[user].withdrawn;
    }

    // Calculate tokens required to redeem for exact WPLS amount
    function calculateTokensForExactWPLS(uint256 _amountOut) public view returns (uint256 _amountIn) {
        require(_amountOut > 0, "WPLS amount must be greater than 0");

        _amountIn = _amountOut.mul(precision).div(_calculatePrice()).mul(feeDenominator).div(sellFee);
    }

    /////////////////////
    // WRITE FUNCTIONS //
    /////////////////////

    // Mint token Tokens For `recipient` By Depositing WPLS Into The Contract
    function mintWithBacking(uint256 numTokens, address recipient) external nonReentrant returns (uint256) {
        _checkGarbageCollector(address(this));
        return _mintWithBacking(numTokens, recipient);
    }

    // Burns Sender's token Tokens and redeems their value in WPLS
    function redeem(uint256 tokenAmount) external nonReentrant returns (uint256) {
        return _sell(msg.sender, tokenAmount, msg.sender);
    }
    
    // Burns Sender's token Tokens and redeems their value in WPLS for `recipient`
    function redeemTo(uint256 tokenAmount, address recipient) external nonReentrant returns (uint256) {
        return _sell(msg.sender, tokenAmount, recipient);
    }
    
    // Allows A User To Erase Their Holdings From Supply - DOES NOT REDEEM UNDERLYING ASSET FOR USER
    function burn(uint256 amount) external nonReentrant {
        uint256 bal = _balances[msg.sender];
        require(bal >= amount && bal > 0, 'Zero Holdings');

        uint256 oldPrice = _calculatePrice();

        _burn(msg.sender, amount);
        _requirePriceRises(oldPrice);

        emit Burn(msg.sender, amount);
    }

    // Approves `spender` to transfer `amount` tokens from caller
    function approve(address spender, uint256 amount) public override returns (bool) {
        _allowances[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }
  
    // Transfer Function
    function transfer(address recipient, uint256 amount) external override nonReentrant returns (bool) {
        return _transferFrom(msg.sender, recipient, amount);
    }

    // Transfer Function
    function transferFrom(address sender, address recipient, uint256 amount) external override nonReentrant returns (bool) {
        _allowances[sender][msg.sender] = _allowances[sender][msg.sender].sub(amount, 'Insufficient Allowance');
        return _transferFrom(sender, recipient, amount);
    }

    ////////////////////////
    // INTERNAL FUNCTIONS //
    ////////////////////////

    // Internal Transfer
    function _transferFrom(address sender, address recipient, uint256 amount) internal returns (bool) {
        require(recipient != address(0) && sender != address(0), "Transfer To Zero");
        require(amount > 0, "Transfer Amt Zero");

        uint256 oldPrice = _calculatePrice();

        _balances[sender] = _balances[sender].sub(amount, "Insufficient Balance");
        _balances[recipient] = _balances[recipient].add(amount);

        _requirePriceRises(oldPrice);
        _total_txs += 1;

        emit Transfer(sender, recipient, amount);
        return true;
    }

    // Stake Tokens and Deposits token in Sender's Address, Must Have Prior Approval For WPLS
    function _mintWithBacking(uint256 amount, address recipient) internal returns (uint256) {
        uint256 userTokenBalance = WPLS.balanceOf(msg.sender);
        require(userTokenBalance > 0 && amount <= userTokenBalance, 'Insufficient Balance');

        uint256 oldPrice = _calculatePrice();
        uint256 amountUnderlying = underlyingBalance();
        uint256 received = _transferIn(amount);

        _stats[msg.sender].lastEntryPrice = oldPrice;
        _stats[msg.sender].deposited = _stats[msg.sender].deposited.add(amount);

        return _mintTo(recipient, received, amountUnderlying, oldPrice);
    }
    
    // Burns token Tokens And Deposits WPLS Tokens into Recipients's Address
    function _sell(address seller, uint256 tokenAmount, address recipient) internal returns (uint256) {
        require(tokenAmount > 0 && _balances[seller] >= tokenAmount);
        require(seller != address(0) && recipient != address(0));

        uint256 oldPrice = _calculatePrice();
        uint256 tokensToSwap = tokenAmount.mul(sellFee).div(feeDenominator);
        uint256 amountUnderlyingAsset = amountOut(tokensToSwap);

        _burn(seller, tokenAmount);

        require(WPLS.transfer(recipient, amountUnderlyingAsset), 'Underlying Transfer Failure');

        _requirePriceRises(oldPrice);
        _total_txs += 1;

        _stats[recipient].withdrawn = _stats[recipient].withdrawn.add(amountUnderlyingAsset);

        emit Redeemed(seller, tokenAmount, amountUnderlyingAsset);
        return amountUnderlyingAsset;
    }

    // Handles Minting Logic To Create New token
    function _mintTo(address recipient, uint256 received, uint256 totalBacking, uint256 oldPrice) private returns(uint256) {
        uint256 tokensToMintNoTax = _totalSupply == 0 ? received : _totalSupply.mul(received).div(totalBacking);
        uint256 tokensToMint = tokensToMintNoTax.mul(mintFee).div(feeDenominator);
        
        require(tokensToMint > 0, 'Zero Amount');

        _mint(recipient, tokensToMint);
        _requirePriceRises(oldPrice);

        _total_txs += 1;

        // differentiate purchase
        emit Minted(recipient, tokensToMint);
        return tokensToMint;
    }

    // Requires The Price Of token To Rise For The Transaction To Conclude
    function _requirePriceRises(uint256 oldPrice) internal {
        uint256 newPrice = _calculatePrice();
        require(newPrice >= oldPrice, 'Price Cannot Fall');
        emit PriceChange(oldPrice, newPrice, _totalSupply);
    }

    // Transfers `desiredAmount` of `token` in and verifies the transaction success
    function _transferIn(uint256 desiredAmount) internal returns (uint256) {
        uint256 balBefore = underlyingBalance();
        require(WPLS.transferFrom(msg.sender, address(this), desiredAmount), 'Failure Transfer From');
        uint256 balAfter = underlyingBalance();
        require(balAfter > balBefore, 'Zero Received');
        return balAfter - balBefore;
    }
    
    // Mints Tokens to the Receivers Address
    function _mint(address receiver, uint amount) private {

        if (_users[receiver] == 0){
            _users[receiver] = 1;
            _total_users += 1;
        }
        
        _balances[receiver] = _balances[receiver].add(amount);
        _totalSupply = _totalSupply.add(amount);
        emit Transfer(address(0), receiver, amount);
    }
    
    // Burns `amount` of tokens from `account`
    function _burn(address account, uint amount) private {
        _balances[account] = _balances[account].sub(amount, 'Insufficient Balance');
        _totalSupply = _totalSupply.sub(amount, 'Negative Supply');
        emit Transfer(account, address(0), amount);
    }

    // Make Sure there's no Native Tokens in contract
    function _checkGarbageCollector(address burnLocation) internal {
        uint256 bal = _balances[burnLocation];
        if (bal > 10**3) {
            uint256 oldPrice = _calculatePrice();
            _burn(burnLocation, bal);

            emit GarbageCollected(bal);
            emit PriceChange(oldPrice, _calculatePrice(), _totalSupply);
        }
    }

    // Returns the Current Price of 1 Token
    function _calculatePrice() internal view returns (uint256) {
        return _totalSupply == 0 ? 10**18 : (underlyingBalance().mul(precision)).div(_totalSupply);
    }
}
```